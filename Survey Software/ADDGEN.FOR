C PROGRAM TO GENERATE RANDOM ADDRESSES FOR SURVEY SIMULATION EXERCISES
C --------------------------------------------------------------------
C
C     C. G. MCLAREN, SIMON FRASER UNIVERSITY, DEC. 1984
C     MODIFIED BY T. CHANG, UNIVERSITY OF KANSAS, FEB. 1985, SEPT. 1986
C                 T. CHANG, UNIVERSITY OF VIRGINIA, APRIL 1991
C                 maintenance fix January 1995
C     COPYRIGHT (C) 1992, T. CHANG AND C. G. MCLAREN
C
C THIS PROGRAM REQUIRES THE FOLLOWING INPUT
C
C     A RANDOM NUMBER GENERATOR SEED: INTEGER BETWEEN 1 AND 1000000.
C     THE LIST OF DISTRICTS TO BE SAMPLED: THE DISTRICT NUMBERS ARE
C  TO BE SEPARATED BY COMMAS; CONSECUTIVE DISTRICTS CAN BE ENTERED
C  BY TYPING FIRST AND LAST DISTRICT NUMBERS SEPARATED BY - (DASH);
C  THE LIST CAN BE CONTINUED TO A NEW LINE BY ENDING THE PREVIOUS LINE WITH
C  A DOLLAR SYMBOL $.
C     THE NUMBER OF DISTRICTS TO BE SAMPLED.
C
C THIS PROGRAM PRODUCES ADDRESSES FOR A SIMPLE RANDOM SAMPLE FROM THE
C LISTED DISTRICTS.  THE LIST OF ADDRESSES TOGETHER WITH THE RANDOM
C START IS PUT IN FILE 11.
C
C REMARK: comment lines below contain modifications for use with
C         Language Systems FORTRAN for the MacIntosh
C         These modifications set the 'creator' on output files;
C         allowing double click opening of output files
C
C**********************************************************************
C
      INTEGER H(1000), DS(75), DIS(75) 
      CHARACTER YES1, YES2,CH
      CHARACTER*8 INUNIT
      INTEGER I,IX,IIX,NADS,NTOT,IDIS,NDIS,N,IH
      DATA DS/142,153,135,128,110,103,105,385,296,287,
     &253,172,198,432,248,251,221,297,235,171,
     &135,254,203,244,202,103,102,115,180,190,
     &152,141,143,135,178,221,174,101,95,130,
     &152,169,91,283,562,312,897,734,963,642,
     &525,726,674,585,553,583,911,1051,918,799,
     &545,895,1313,968,717,651,886,912,898,759,
     &722,753,793,725,802/
      DATA YES1/'y'/, YES2/'Y'/
C
22    WRITE(*,1100)
1100  FORMAT(' ENTER FILENAME FOR ADDRESS SET--8 OR FEWER LETTERS')
      READ(*,'(A)',END=22,ERR=22) INUNIT
      OPEN(UNIT=11,FILE=INUNIT)
c in Language Systems FORTRAN for the Mac use the following instead
c      OPEN(UNIT=11,FILE=INUNIT,CREATOR='EDIT')
c      creator 'ttxt' for teach text
 5    WRITE(*,109)
 109  FORMAT(' ENTER RANDOM START--ANY INTEGER BETWEEN 1 AND 1000000')
      READ(*,*,END=5,ERR=5) IX
      IF (IX.LT.1.OR.IX.GT.1000000) GO TO 5
      IIX = IX
C
      NADS=0
 20   WRITE(*,104)
 104  FORMAT(' ENTER DISTRICTS FROM WHICH YOU WISH TO SAMPLE')
      CALL RDIS(NDIS,DIS)
      CALL ISRTA(DIS,NDIS)
      NTOT=0
      DO 1 I=1,NDIS
 1    NTOT=NTOT+DS(DIS(I))
      WRITE(*,122) NDIS, NTOT
 122  FORMAT(I8,' DISTRICTS WITH ',I7,' HOUSEHOLDS HAVE BEEN SPECIFIED')
 4    WRITE(*,105)
 105  FORMAT(' ENTER NUMBER OF ADDRESSES TO BE GENERATED (MAX 1000)')
      READ(*,*,END=4,ERR=4) N
      IF (N.GT.1000.OR.N.LT.1) GO TO 4
      IF (N.GT.NTOT) GO TO 905
      CALL NRSAM(N,NTOT,H,IX)
      IDIS=1
      NTOT=0
      DO 8 I=1,N
 6    IF (H(I)-NTOT.LE.DS(DIS(IDIS))) GO TO 7
      NTOT=NTOT+DS(DIS(IDIS))
      IDIS=IDIS+1
      GO TO 6
 7    IH=H(I)-NTOT
      WRITE(11,101) DIS(IDIS),IH
 8    CONTINUE
 101  FORMAT(I4,I6,I25)
      NADS=NADS+N
      WRITE(*,120)
 120  FORMAT(' DO YOU WANT TO SPECIFY A NEW DISTRICT SET'/' ANSWER
     & YES OR NO')
      READ(*,121) CH
 121  FORMAT(A1)
      IF (CH.EQ.YES1.OR.CH.EQ.YES2) GO TO 20
      IH=0
      WRITE(11,101) IH,IH,IIX
      WRITE(*,108) NADS,IIX
 108  FORMAT(' ',I5,' RANDOM ADDRESSES GENERATED WITH RANDOM START',I8)
      CLOSE(11)
      STOP
C
 905  WRITE(*,111) N, NTOT
 111  FORMAT(' SAMPLE SIZE ',I4,' IS GREATER THAN POPULATION SIZE '
     &, I6,' --REENTER.')
      GO TO 4
      END
C
C***********************************************************************
C
      SUBROUTINE RDIS(NDIS,DIS)
C SUBROUTINE TO READ A DISTRICT SPECIFICATION LIST
      INTEGER NUM(2),IVAL(2),DIS(1)
      CHARACTER*1 ICHR(14),LIN(80)
      INTEGER I,J,K,L,M,MM,NDASH,NDIS
      DATA ICHR/'0','1','2','3','4','5','6','7','8','9',' ',',','-','$'/
 1    NDIS=0
      L=1
      K=0
      NDASH=0
 18   READ(*,100) LIN
 100  FORMAT(80A1)
      DO 20 J=1,80
      DO 2 I=1,14
      IF (LIN(J).EQ.ICHR(I)) GO TO 3
 2    CONTINUE
      WRITE(*,101) LIN(J)
 101  FORMAT(' ILLEGAL CHARACTER ',A1,' REENTER LIST.')
      GO TO 1
 3    IF (I.EQ.11.AND.J.LT.80) GO TO 20
      IF (I.EQ.11.AND.J.EQ.80) I=12
      IF (I.GT.10) GO TO 5
      IF (K.LT.2) GO TO 4
      WRITE(*,102) NUM(1), NUM(2), LIN(J)
 102  FORMAT(' 3 DIGIT NUMBER - ',2I1,A1,' ILLEGAL--REENTER LIST')
      GO TO 1
 4    K=K+1
      NUM(K)=I-1
      GO TO 20
 5    IF (I.EQ.14) GO TO 18
      IF (K.GT.0) GO TO 6
      WRITE(*,103)
 103  FORMAT(' NO NUMBER BETWEEN SEPARATORS--REENTER LIST')
      GO TO 1
 6    IF (I.EQ.12) GO TO 7
      NDASH=NDASH+1
      IF (NDASH.EQ.1) GO TO 8
      WRITE(*,104)
 104  FORMAT(' TWO CONSECUTIVE - SEPARATORS NOT ALLOWED--REENTER LIST')
      GO TO 1
 7    IF (NDASH.EQ.1) L=2
 8    IVAL(L)=0
      DO 9 I=1,K
      M=K-I
 9    IVAL(L)=IVAL(L)+NUM(I)*10**M
      K=0
      IF (IVAL(L).GE.1.AND.IVAL(L).LE.75) GO TO 10
      WRITE(*,105) IVAL(L)
 105  FORMAT(' DISTRICT NUMBER ',I5,' OUT OF RANGE--REENTER LIST')
      GO TO 1
 10   IF (L.EQ.2) GO TO 13
      IF (NDASH.EQ.1) GO TO 20
      IF (NDIS.EQ.0) GO TO 12
      DO 11 I=1,NDIS
      IF (IVAL(L).NE.DIS(I)) GO TO 11
      WRITE (9,106) DIS(I)
      GO TO 1
 11   CONTINUE
 106  FORMAT(' DISTRICT ',I5,' REPEATED--REENTER LIST')
 12   NDIS=NDIS+1
      DIS(NDIS)=IVAL(L)
      GO TO 20
 13   IF (IVAL(1).LT.IVAL(2)) GO TO 14
      WRITE(*,107) IVAL(1),IVAL(2)
 107  FORMAT(' DISTRICTS',I5,' TO ',I5,' NOT ALLOWED--REENTER LIST')
      GO TO 1
 14   IF (NDIS.EQ.0) GO TO 16
      DO 15 I=1,NDIS
      IF (DIS(I).LT.IVAL(1).OR.DIS(I).GT.IVAL(2)) GO TO 15
      WRITE(*,106) DIS(I)
      GO TO 1
 15   CONTINUE
 16   M=IVAL(1)
      MM=IVAL(2)
      DO 17 I=M,MM
      NDIS=NDIS+1
 17   DIS(NDIS)=I
      NDASH=0
      L=1
 20   CONTINUE
      RETURN
      END
C
C*************************************************************************
C
      SUBROUTINE NRSAM(N,NTOT,NOS,IX)
C SUBROUTINE TO GENERATE A RANDOM NON REPLACEMENT SAMPLE
      INTEGER NOS(1),KL(1000)
      INTEGER I,J,K,L,M,N,NN,NTOT,IX
      REAL X,RANDU
      IF (N.LT.NTOT) GO TO 2
      DO 1 I=1,NTOT
 1    NOS(I)=I
      RETURN
 2    NN=N
      IF (N.GT.NTOT/2) NN=NTOT-N
      X=RANDU(IX)
      NOS(1)=MOD(IX,NTOT)+1
      DO 7 I=2,NN
      K=I-1
 3    X=RANDU(IX)
      NOS(I)=MOD(IX,NTOT)+1
      DO 4 L=1,K
      IF (NOS(I).LT.NOS(L)) GO TO 5
      IF (NOS(I).EQ.NOS(L)) GO TO 3
 4    CONTINUE
      GO TO 7
 5    K=K-L+1
      J=NOS(I)
      DO 6 M=1,K
 6    NOS(I-M+1)=NOS(I-M)
      NOS(L)=J
 7    CONTINUE
      IF (N.LE.NTOT/2) RETURN
      DO 8 I=1,NN
 8    KL(I)=NOS(I)
      KL(NN+1)=NTOT+1
      J=1
      K=1
      DO 10 I=1,NTOT
      IF (I.LT.KL(J)) GO TO 9
      J=J+1
      GO TO 10
 9    NOS(K)=I
      K=K+1
 10   CONTINUE
      RETURN
      END
C
C**************************************************************************
C
      SUBROUTINE ISRTA(Y,M)
C SUBROUTINE TO SORT M ELEMENTS IN ARRAY Y INTO ASCENDING ORDER
C
      INTEGER M,MM,I,K,KK,L
      INTEGER Y(M), Z
      IF (M.LE.1) RETURN
      MM=M-1
      DO 4 I=1,MM
      Z=Y(I+1)
      DO 1 K=1,I
      IF (Z-Y(I+1-K)) 1,1,2
 1    CONTINUE
      K=I+1
 2    KK=K-1
      IF (KK.EQ.0) GO TO 4
      DO 3 L=1,KK
 3    Y(I+2-L)=Y(I+1-L)
      Y(I+2-K)=Z
 4    CONTINUE
      RETURN
      END
C
C**************************************************************************
C
      REAL FUNCTION RANDU(IX)
C
C UNIFORM RANDOM NUMBER GENERATOR.
C REF: SCHRAGE, L. 1979 'A MORE PORTABLE FORTRAN RANDOM NUMBER GENERATOR'
C      A. C. M. TRANSACTIONS ON MATHEMATICAL SOFTWARE V5 132-138.
C
      INTEGER A,P,IX,B15,B16,XHI,XALO,LEFTLO,FHI,K
      DATA A/16807/,B15/32768/,B16/65536/,P/2147483647/
      XHI=IX/B16
      XALO=(IX-XHI*B16)*A
      LEFTLO=XALO/B16
      FHI=XHI*A+LEFTLO
      K=FHI/B15
      IX=(((XALO-LEFTLO*B16)-P)+(FHI-K*B15)*B16)+K
      IF (IX.LT.0) IX=IX+P
      RANDU=FLOAT(IX)*4.656612875E-10
      RETURN
      END
